/*
Copyright (c) 2019 Red Hat, Inc.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

  http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

// Manages the collection of clusters.
resource Clusters {
	// Retrieves the list of clusters.
	method List {
		// Index of the requested page, where one corresponds to the first page.
		//
		// Default value is `1`.
		in out Page Integer

		// Maximum number of items that will be contained in the returned page.
		//
		// Default value is `100`.
		in out Size Integer

		// Search criteria.
		// 
		// The syntax of this parameter is similar to the syntax of the _where_ clause
		// of an SQL statement, but using the names of the attributes of the cluster
		// instead of the names of the columns of a table. For example, in order to
		// retrieve all the clusters with a name starting with `my` in the
		// `us-east-1` region the value should be:
		//
		// [source,sql]
		// ----
		// name like 'my%' and region.id = 'us-east-1'
		// ----
		//
		// If the parameter isn't provided, or if the value is empty, then all the
		// clusters that the user has permission to see will be returned.
		in Search String

		// Total number of items of the collection that match the search criteria,
		// regardless of the size of the page.
		in out Total Integer

		// Retrieved list of clusters.
		out Items []Cluster
	}

	// Provision a new cluster and add it to the collection of clusters.
	// 
	// See the `register_cluster` method for adding an existing cluster.
	method Add {
		// Description of the cluster.
		in out Body Cluster
	}

	// Retursn a reference to the service that manages an specific cluster.
	locator Cluster {
		target Cluster
		variable ID
	}
}
