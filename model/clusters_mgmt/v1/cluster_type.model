/*
Copyright (c) 2019 Red Hat, Inc.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

  http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

// Definition of an _OpenShift_ cluster.
//
// The `cloud_provider` attribute is a reference to the cloud provider. When a
// cluster is retrieved it will be a link to the cloud provider, containing only
// the kind, id and href attributes:
//
// [source,json]
// ----
// {
//   "cloud_provider": {
//     "kind": "CloudProviderLink",
//     "id": "123",
//     "href": "/api/clusters_mgmt/v1/cloud_providers/123"
//   }
// }
// ----
//
// When a cluster is created this is optional, and if used it should contain the
// identifier of the cloud provider to use:
//
// [source,json]
// ----
// {
//   "cloud_provider": {
//     "id": "123",
//   }
// }
// ----
//
// If not included, then the cluster will be created using the default cloud
// provider, which is currently Amazon Web Services.
//
// The region attribute is mandatory when a cluster is created.
//
// The `aws.access_key_id`, `aws.secret_access_key` and `dns.base_domain`
// attributes are mandatory when creation a cluster with your own Amazon Web
// Services account.
class Cluster {
	// Name of the cluster. This name is assigned by the user when the
	// cluster is created.
	Name String

	// Link to the _flavour_ that was used to create the cluster.
	link Flavour Flavour

	// Information about the console of the cluster.
	Console ClusterConsole

	// Flag indicating if the cluster should be created with nodes in
	// different availability zones or all the nodes in a single one
	// randomly selected.
	MultiAZ Boolean

	// Information about the nodes of the cluster.
	Nodes ClusterNodes

	// Information about the API of the cluster.
	API ClusterAPI

	// Link to the cloud provider region where the cluster is installed.
	link Region CloudRegion

	// Name of the cluster for display purposes. It can contain any
	// characters, including spaces.
	DisplayName String

	// DNS settings of the cluster.
	DNS DNS

	// User defined properties for tagging and querying.
	Properties [String]String

	// Overall state of the cluster.
	State ClusterState

	// Flag indicating if the cluster is managed (by Red Hat) or
	// self-managed by the user.
	Managed Boolean

	// External identifier of the cluster, generated by the installer.
	ExternalID String

	// Amazon Web Services settings of the cluster.
	AWS AWS

	// Google cloud platform settings of the cluster.
	GCP GCP

	// Network settings of the cluster.
	Network Network

	// Date and time when the cluster was initially created, using the
	// format defined in https://www.ietf.org/rfc/rfc3339.txt[RC3339].
	CreationTimestamp Date

	// Date and time when the cluster will be automatically deleted, using the format defined in
	// https://www.ietf.org/rfc/rfc3339.txt[RFC3339]. If no timestamp is provided, the cluster
	// will never expire.
	//
	// This option is unsupported.
	ExpirationTimestamp Date

	// Link to the cloud provider where the cluster is installed.
	link CloudProvider CloudProvider

	// Version of _OpenShift_ installed in the cluster, for example `4.0.0-0.2`.
	//
	// When retrieving a cluster this will always be reported.
	//
	// When provisioning a cluster this will be ignored, as the version to
	// deploy will be determined internally.
	OpenshiftVersion String

	// Link to the subscription that comes from the account management service when the cluster
	// is registered.
	link Subscription Subscription

	// Link to the collection of groups of user of the cluster.
	link Groups []Group

	// Link to the version of _OpenShift_ that will be used to install the cluster.
	link Version Version

	// Link to the collection of identity providers of the cluster.
	link IdentityProviders []IdentityProvider

	// Cluster metrics received from telemetry.
	//
	// When provisioning a cluster this will be ignored.
	Metrics ClusterMetrics

	// Contains configuration of a Customer Cloud Subscription cluster.
	CCS CCS

	// List of add-ons on this cluster.
	link Addons []AddOnInstallation

	// List of machine pools on this cluster.
	link MachinePools []MachinePool

	// List of ingresses on this cluster.
	link Ingresses []Ingress

	// List of AWS infrastructure access role grants on this cluster.
	link AWSInfrastructureAccessRoleGrants []AWSInfrastructureAccessRoleGrant

	// Storage quota to be assigned to the cluster.
	StorageQuota Value

	// Load Balancer quota to be assigned to the cluster.
	LoadBalancerQuota Integer

	// Flag indicating if cluster-admin access is enabled.
	ClusterAdminEnabled Boolean

	// Link to the product type of this cluster.
	link Product Product

	// HealthState indicates the overall health state of the cluster.
	HealthState ClusterHealthState

	// ExternalConfiguration shows external configuration on the cluster.
	ExternalConfiguration ExternalConfiguration

	// ProvisionShard contains the properties of the provision shard, including AWS and GCP related configurations
	ProvisionShard ProvisionShard

	//Status of cluster
	Status ClusterStatus
}
